GFX_Char_Billy_Idle0: INCBIN "data/gfx/char/billy_idle0.bin"
GFX_Char_Billy_Idle1: INCBIN "data/gfx/char/billy_idle1.bin"
GFX_Char_Billy_Idle2: INCBIN "data/gfx/char/billy_idle2.bin"
GFX_Char_Billy_WalkF0: INCBIN "data/gfx/char/billy_walkf0.bin"
GFX_Char_Billy_WalkF1_A: INCBIN "data/gfx/char/billy_walkf1_a.bin"
GFX_Char_Billy_WalkF1_B: INCBIN "data/gfx/char/billy_walkf1_b.bin"
GFX_Char_Billy_CrouchWalkF0_A: INCBIN "data/gfx/char/billy_crouchwalkf0_a.bin"
GFX_Char_Billy_CrouchWalkF0_B: INCBIN "data/gfx/char/billy_crouchwalkf0_b.bin"
GFX_Char_Billy_CrouchWalkF1_B: INCBIN "data/gfx/char/billy_crouchwalkf1_b.bin"
GFX_Char_Billy_CrouchWalkF2: INCBIN "data/gfx/char/billy_crouchwalkf2.bin"
GFX_Char_Billy_BlockG0_A: INCBIN "data/gfx/char/billy_blockg0_a.bin"
GFX_Char_Billy_BlockC0_A: INCBIN "data/gfx/char/billy_blockc0_a.bin"
GFX_Char_Billy_Hit0Mid0_A: INCBIN "data/gfx/char/billy_hit0mid0_a.bin"
GFX_Char_Billy_Hit0Mid0_B: INCBIN "data/gfx/char/billy_hit0mid0_b.bin"
GFX_Char_Billy_Hit1Mid1_A: INCBIN "data/gfx/char/billy_hit1mid1_a.bin"
GFX_Char_Billy_HitLow0_A: INCBIN "data/gfx/char/billy_hitlow0_a.bin"
GFX_Char_Billy_LaunchUB2: INCBIN "data/gfx/char/billy_launchub2.bin"
GFX_Char_Billy_LaunchUB1: INCBIN "data/gfx/char/billy_launchub1.bin"
GFX_Char_Billy_PunchLN0_A: INCBIN "data/gfx/char/billy_punchln0_a.bin"
GFX_Char_Billy_PunchLN0_B: INCBIN "data/gfx/char/billy_punchln0_b.bin"
GFX_Char_Billy_PunchLN1_A: INCBIN "data/gfx/char/billy_punchln1_a.bin"
GFX_Char_Billy_PunchLM0: INCBIN "data/gfx/char/billy_punchlm0.bin"
GFX_Char_Billy_PunchLM1_A: INCBIN "data/gfx/char/billy_punchlm1_a.bin"
GFX_Char_Billy_PunchHN1: INCBIN "data/gfx/char/billy_punchhn1.bin"
GFX_Char_Billy_PunchHM0: INCBIN "data/gfx/char/billy_punchhm0.bin"
GFX_Char_Billy_PunchHM2: INCBIN "data/gfx/char/billy_punchhm2.bin"
GFX_Char_Billy_KickLN1: INCBIN "data/gfx/char/billy_kickln1.bin"
GFX_Char_Billy_KickHN1: INCBIN "data/gfx/char/billy_kickhn1.bin"
GFX_Char_Billy_KickHM1: INCBIN "data/gfx/char/billy_kickhm1.bin"
GFX_Char_Billy_PunchCL0_A: INCBIN "data/gfx/char/billy_punchcl0_a.bin"
GFX_Char_Billy_PunchCL1_A: INCBIN "data/gfx/char/billy_punchcl1_a.bin"
GFX_Char_Billy_PunchCH1: INCBIN "data/gfx/char/billy_punchch1.bin"
GFX_Char_Billy_KickCL1: INCBIN "data/gfx/char/billy_kickcl1.bin"
GFX_Char_Billy_KickCH1: INCBIN "data/gfx/char/billy_kickch1.bin"
GFX_Char_Billy_PunchALI0_B: INCBIN "data/gfx/char/billy_punchali0_b.bin"
GFX_Char_Billy_PunchALX0: INCBIN "data/gfx/char/billy_punchalx0.bin"
GFX_Char_Billy_PunchAHX0: INCBIN "data/gfx/char/billy_punchahx0.bin"
GFX_Char_Billy_KickALI0: INCBIN "data/gfx/char/billy_kickali0.bin"
GFX_Char_Billy_KickHM0: INCBIN "data/gfx/char/billy_kickhm0.bin"
GFX_Char_Billy_ChargeMeter0_A: INCBIN "data/gfx/char/billy_chargemeter0_a.bin"
GFX_Char_Billy_BlockC0_B: INCBIN "data/gfx/char/billy_blockc0_b.bin"
GFX_Char_Billy_ChargeMeter1_A: INCBIN "data/gfx/char/billy_chargemeter1_a.bin"
GFX_Char_Billy_AttackA0: INCBIN "data/gfx/char/billy_attacka0.bin"
GFX_Char_Billy_Taunt0_A: INCBIN "data/gfx/char/billy_taunt0_a.bin"
GFX_Char_Billy_Taunt0_B: INCBIN "data/gfx/char/billy_taunt0_b.bin"
GFX_Char_Billy_Taunt1_A: INCBIN "data/gfx/char/billy_taunt1_a.bin"
GFX_Char_Billy_Win7_A: INCBIN "data/gfx/char/billy_win7_a.bin"
GFX_Char_Billy_SansetsuKonChuudanUchi1_A: INCBIN "data/gfx/char/billy_sansetsukonchuudanuchi1_a.bin"
GFX_Char_Billy_SansetsuKonChuudanUchi1_B: INCBIN "data/gfx/char/billy_sansetsukonchuudanuchi1_b.bin"
GFX_Char_Billy_SansetsuKonChuudanUchi2_B: INCBIN "data/gfx/char/billy_sansetsukonchuudanuchi2_b.bin"
GFX_Char_Billy_Intro1: INCBIN "data/gfx/char/billy_intro1.bin"
GFX_Char_Billy_Intro2: INCBIN "data/gfx/char/billy_intro2.bin"
GFX_Char_Billy_Intro3: INCBIN "data/gfx/char/billy_intro3.bin"
GFX_Char_Billy_SuzumeOtoshi1_A: INCBIN "data/gfx/char/billy_suzumeotoshi1_a.bin"
GFX_Char_Billy_SuzumeOtoshi1_B: INCBIN "data/gfx/char/billy_suzumeotoshi1_b.bin"
GFX_Char_Billy_KyoushuuHishouKon2: INCBIN "data/gfx/char/billy_kyoushuuhishoukon2.bin"
GFX_Char_Billy_KyoushuuHishouKon4: INCBIN "data/gfx/char/billy_kyoushuuhishoukon4.bin"
GFX_Char_Billy_KyoushuuHishouKon5: INCBIN "data/gfx/char/billy_kyoushuuhishoukon5.bin"
GFX_Char_Billy_KyoushuuHishouKon6: INCBIN "data/gfx/char/billy_kyoushuuhishoukon6.bin"
GFX_Char_Billy_ChouKaenSenpuuKon2: INCBIN "data/gfx/char/billy_choukaensenpuukon2.bin"
GFX_Char_Billy_ChouKaenSenpuuKon3: INCBIN "data/gfx/char/billy_choukaensenpuukon3.bin"
GFX_Char_Billy_ChouKaenSenpuuKon4: INCBIN "data/gfx/char/billy_choukaensenpuukon4.bin"
GFX_Char_Billy_KickFH2: INCBIN "data/gfx/char/billy_kickfh2.bin"

; =============== MoveC_Ralf_ThrowG ===============
; Move code for Ralf's ground throw. (MOVE_SHARED_THROW_G).
; See also: MoveC_Kyo_ThrowG
MoveC_Ralf_ThrowG:
	mMvC_ValLoaded .ret
	; Depending on the visible frame...
	mMvC_StartChkFrame
		mMvC_ChkFrame $00, .obj0
		mMvC_ChkFrame $01, .obj1
		mMvC_ChkFrame $02, .anim
		mMvC_ChkFrame $03, .chkEnd
	jp   .anim
; --------------- frame #0 ---------------
.obj0:
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $06, HITTYPE_GRAB_UB_NOSYNC, PF3_HEAVYHIT
		jp   .anim
; --------------- frame #1 ---------------
.obj1:
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $06, HITTYPE_LAUNCH_FAST_DB, PF3_HEAVYHIT
		jp   .anim
; --------------- frame #3 ---------------
.chkEnd:
	; Wait for the animation to advance before ending the move
	mMvC_ValFrameEnd .anim
		; And when it does, also reset the throw sequence
		mMvC_EndThrow
		jr   .ret
; --------------- common ---------------
.anim:
	jp   OBJLstS_DoAnimTiming_Loop_by_DE
.ret:
	ret
	
; =============== MoveInputReader_Ralf ===============
; Special move input checker for RALF.
; IN
; - BC: Ptr to wPlInfo
; - DE: Ptr to respective wOBJInfo
; OUT
; - C flag: If set, a move was started
MoveInputReader_Ralf:
	mMvIn_Validate Ralf, 2
	
.chkGround:
	;             SELECT + B                         SELECT + A
	mMvIn_ChkEasy MoveInit_Ralf_BaribariVulcanPunch, MoveInit_Ralf_VulcanPunch
	mMvIn_ChkGA Ralf, .chkPunch, .chkKick, CHKGA_KICK|CHKGA_PUNCH

.chkPunch:
	mMvIn_ValSuper .chkPunchNoSuper
	; (DB)BF+P -> Baribari Vulcan Punch (Super Vulcan Punch)
	mMvIn_ChkDir MoveInput_1BF_Charge, MoveInit_Ralf_BaribariVulcanPunch
.chkPunchNoSuper:
	; DU+P -> Kyuukouka Bakudan Punch (Diagonal Punch)
	mMvIn_ChkDir MoveInput_DU_Charge, MoveInit_Ralf_BakudanPunch
	; BF+P -> Gatling Attack
	mMvIn_ChkDir MoveInput_BF_Charge, MoveInit_Ralf_GatlingAttack
	; PPP -> Vulcan Punch (Evil Checkers)
	mMvIn_ChkBtnStrict MoveInput_PPP, MoveInit_Ralf_VulcanPunch
	jp   MoveInputReader_Ralf_NoMove
.chkKick:
	; BDF+K -> Super Argentine Back Breaker (Game Breaker)
	mMvIn_ChkDir MoveInput_BDF, MoveInit_Ralf_BackBreaker
	jp   MoveInputReader_Ralf_NoMove
	
; =============== MoveInit_Ralf_VulcanPunch ===============
MoveInit_Ralf_VulcanPunch:
	call Play_Pl_ClearJoyBtnBuffer
	mMvIn_GetLHP MOVE_RALF_VULCAN_PUNCH_L, MOVE_RALF_VULCAN_PUNCH_H
	call MoveInputS_SetSpecMove_StopSpeed
	jp   MoveInputReader_Ralf_MoveSet
	
; =============== MoveInit_Ralf_GatlingAttack ===============
MoveInit_Ralf_GatlingAttack:
	call Play_Pl_ClearJoyDirBuffer
	mMvIn_GetLHP MOVE_RALF_GATLING_ATTACK_L, MOVE_RALF_GATLING_ATTACK_H
	call MoveInputS_SetSpecMove_StopSpeed
	ld   hl, iPlInfo_Flags1
	add  hl, bc
	set  PF1B_INVULN, [hl]
	jp   MoveInputReader_Ralf_MoveSet
	
; =============== MoveInit_Ralf_BackBreaker ===============
MoveInit_Ralf_BackBreaker:
	call Play_Pl_ClearJoyDirBuffer
	mMvIn_ValStartCmdThrow_StdColi Ralf
		mMvIn_GetLHK MOVE_RALF_BACK_BREAKER_L, MOVE_RALF_BACK_BREAKER_H
		call MoveInputS_SetSpecMove_StopSpeed
		ld   hl, iPlInfo_Flags1
		add  hl, bc
		set  PF1B_INVULN, [hl]
		jp   MoveInputReader_Ralf_MoveSet
	
; =============== MoveInit_Ralf_BakudanPunch ===============
MoveInit_Ralf_BakudanPunch:
	call Play_Pl_ClearJoyDirBuffer
	mMvIn_GetLHP MOVE_RALF_BAKUDAN_PUNCH_L, MOVE_RALF_BAKUDAN_PUNCH_H
	call MoveInputS_SetSpecMove_StopSpeed
	ld   hl, iPlInfo_Flags1
	add  hl, bc
	set  PF1B_INVULN, [hl]
	jp   MoveInputReader_Ralf_MoveSet
	
; =============== MoveInit_Ralf_BaribariVulcanPunch ===============
MoveInit_Ralf_BaribariVulcanPunch:
	call Play_Pl_ClearJoyDirBuffer
	ld   a, MOVE_RALF_BARIBARI_VULCAN_PUNCH_S
	call MoveInputS_SetSpecMove_StopSpeed
	ld   hl, iPlInfo_Flags1
	add  hl, bc
	set  PF1B_INVULN, [hl]
	jp   MoveInputReader_Ralf_MoveSet
	
; =============== MoveInputReader_Ralf_MoveSet ===============
MoveInputReader_Ralf_MoveSet:
	scf  
	ret  
; =============== MoveInputReader_Ralf_NoMove ===============
MoveInputReader_Ralf_NoMove:
	or   a
	ret  
	
; =============== MoveC_Ralf_VulcanPunch ===============
; Move code for Ralf's Vulcan Punch. (MOVE_RALF_VULCAN_PUNCH_L, MOVE_RALF_VULCAN_PUNCH_H)
MoveC_Ralf_VulcanPunch:
	call Play_Pl_MoveByColiBoxOverlapX
	mMvC_ValLoaded .ret
	; Depending on the visible frame...
	mMvC_StartChkFrame
		mMvC_ChkFrame $00, .init
		mMvC_ChkFrame $03, .chkEnd
	jp   .damageLoop
; --------------- frame #0 ---------------
.init:
	; Initialize the loop count.
	; At Max Power, the move lasts twice as long.
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed ANIMSPEED_INSTANT
		mMvC_ChkMaxPow .initMaxPower
	.initNorm:
		ld   hl, iPlInfo_Ralf_VulcanPunch_LoopCount
		add  hl, bc
		ld   [hl], $08
		jp   .setDamage
	.initMaxPower:
		ld   hl, iPlInfo_Ralf_VulcanPunch_LoopCount
		add  hl, bc
		ld   [hl], $10
		jp   .setDamage
; --------------- frames #1-2 ---------------
.damageLoop:
	mMvC_ValFrameEnd .chkMove
		; Deal damage at the end of the frame
		mMvC_PlaySound SCT_GROUNDHIT
		jp   .setDamage
; --------------- frame #3 ---------------
.chkEnd:
	mMvC_ValFrameEnd .chkMove
		; If the counter didn't elapse, loop back to #1.
		; Otherwise, end the move immediately.
		ld   hl, iPlInfo_Ralf_VulcanPunch_LoopCount
		add  hl, bc
		dec  [hl]
		jp   z, .end
		; When looping, deal damage at the end of the frame
		mMvC_SetFrameOnEnd $01
		jp   .setDamage
; --------------- common damage / movement code ---------------
.setDamage:
	;
	; Deal damage to the opponent on contact, which causes a dropdown.
	; The Max Power version is particularly nasty, as it's flagged with PF3_CONTHIT,
	; alliowing juggles.
	;
	mMvC_ChkMaxPow .setDamageMaxPow
.setDamageNorm:
	mMvC_SetDamageNext $08, HITTYPE_LAUNCH_HIGH_UB, PF3_HEAVYHIT|PF3_FIRE
	jp   .chkMove
.setDamageMaxPow:
	mMvC_SetDamageNext $02, HITTYPE_LAUNCH_HIGH_UB, PF3_FIRE|PF3_CONTHIT
	jp   .chkMove
.chkMove:
	;
	; The player can only move forward in the middle of the move.
	;
	ld   hl, iPlInfo_JoyKeys
	add  hl, bc
	bit  KEYB_RIGHT, [hl]	; Holding Right?
	jp   nz, .chkMoveR		; If so, jump
	bit  KEYB_LEFT, [hl]	; Holding Left?
	jp   nz, .chkMoveL		; If so, jump
	jp   .anim
.chkMoveR:
	; To move forwards while holding right, we must be facing right
	ld   hl, iOBJInfo_OBJLstFlags
	add  hl, de
	bit  SPRB_XFLIP, [hl]	; Player facing left? (flag not set)
	jp   z, .anim			; If so, jump
	mMvC_ChkMaxPow .setMoveRMaxPow
.setMoveRNorm:
	mMvC_SetMoveHAbs +$0040 ; move right 0.25px/frame
	jp   .anim
.setMoveRMaxPow:
	mMvC_SetMoveHAbs +$0100 ; move right 1px/frame
	jp   .anim
.chkMoveL:
	; To move forwards while holding left, we must be facing left
	ld   hl, iOBJInfo_OBJLstFlags
	add  hl, de
	bit  SPRB_XFLIP, [hl]	; Player facing right? (flag set)
	jp   nz, .anim			; If so, jump
	mMvC_ChkMaxPow .setMoveLMaxPow
.setMoveLNorm:
	mMvC_SetMoveHAbs -$0040 ; move left 0.25px/frame
	jp   .anim
.setMoveLMaxPow:
	mMvC_SetMoveHAbs -$0100 ; move left 1px/frame
	jp   .anim
; --------------- common ---------------
.end:
	call Play_Pl_EndMove
	jp   .ret
.anim:
	jp   OBJLstS_DoAnimTiming_Loop_by_DE
.ret:
	ret
	
; =============== MoveC_Ralf_VulcanPunch ===============
; Move code for Ralf's Gatling Attack. (MOVE_RALF_GATLING_ATTACK_L, MOVE_RALF_GATLING_ATTACK_H).
; Ralf rotates aggresively while moving forwards.
MoveC_Ralf_GatlingAttack:
	call Play_Pl_MoveByColiBoxOverlapX
	mMvC_ValLoaded .ret
	; Depending on the visible frame...
	mMvC_StartChkFrame
		mMvC_ChkFrame $00, .obj0
		mMvC_ChkFrame $01, .obj1
		mMvC_ChkFrame $02, .obj2
		mMvC_ChkFrame $03, .obj3
		mMvC_ChkFrame $04, .obj4
		mMvC_ChkFrame $05, .chkEnd
; --------------- frame #0 ---------------
; Initial dash forwards.
.obj0:
	mMvC_ValFrameStartFast .obj0_cont
		; Set initial forwards speed
		mMvC_ChkMove MOVE_RALF_GATLING_ATTACK_H, .obj0_setSpeedH
	.obj0_setSpeedL: ; Light
		ld   hl, +$0200
		jp   .obj0_setSpeed
	.obj0_setSpeedH: ; Heavy
		mMvC_ChkMaxPow .obj0_setSpeedE
		ld   hl, +$0300
		jp   .obj0_setSpeed
	.obj0_setSpeedE: ; Max Power Heavy
		ld   hl, +$0400
	.obj0_setSpeed:
		call Play_OBJLstS_SetSpeedH_ByXFlipR
.obj0_cont:
	mMvC_ChkFrictionH $0040, .anim
		; Reset the frame timer if stopping
		ld   hl, iOBJInfo_FrameLeft
		add  hl, de
		ld   [hl], $00
		jp   .anim
; --------------- frame #2 ---------------
; Dash punch.
.obj2:
	mMvC_ValFrameStartFast .obj2_cont
		; Set forwards speed, same as #0
		mMvC_ChkMove MOVE_RALF_GATLING_ATTACK_H, .obj2_setSpeedH
	.obj2_setSpeedL: ; Light
		ld   hl, +$0200
		jp   .obj2_setSpeed
	.obj2_setSpeedH: ; Heavy
		mMvC_ChkMaxPow .obj2_setSpeedE
		ld   hl, +$0300
		jp   .obj2_setSpeed
	.obj2_setSpeedE: ; Max Power Heavy
		ld   hl, +$0400
	.obj2_setSpeed:
		call Play_OBJLstS_SetSpeedH_ByXFlipR
.obj2_cont:
	mMvC_ChkFrictionH $0040, .anim
		; Reset the frame timer if stopping
		ld   hl, iOBJInfo_FrameLeft
		add  hl, de
		ld   [hl], $00
		; Set next punch damage
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID1, PF3_CONTHIT
		jp   .anim
; --------------- frame #4 ---------------
; Dash punch.
.obj4:
	mMvC_ValFrameStartFast .obj4_cont
		; Set forwards speed, same as #0 except for an extra 0.5
		mMvC_ChkMove MOVE_RALF_GATLING_ATTACK_H, .obj4_setSpeedH
	.obj4_setSpeedL: ; Light
		ld   hl, +$0280
		jp   .obj4_setSpeed
	.obj4_setSpeedH: ; Heavy
		mMvC_ChkMaxPow .obj4_setSpeedE
		ld   hl, +$0380
		jp   .obj4_setSpeed
	.obj4_setSpeedE: ; Max Power Heavy
		ld   hl, +$0480
	.obj4_setSpeed:
		call Play_OBJLstS_SetSpeedH_ByXFlipR
.obj4_cont:
	mMvC_ChkFrictionH $0040, .anim
		; Reset the frame timer if stopping
		ld   hl, iOBJInfo_FrameLeft
		add  hl, de
		ld   [hl], $00
		mMvC_SetAnimSpeed $14
		; Final uppercut
		mMvC_SetDamageNext $08, HITTYPE_LAUNCH_HIGH_UB, PF3_CONTHIT
		jp   .anim
; --------------- frame #1 ---------------
; Punch.
.obj1:
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID0, PF3_CONTHIT
		;--
		; Remove invuln
		ld   hl, iPlInfo_Flags1
		add  hl, bc
		res  PF1B_INVULN, [hl]
		;--
		jp   .anim
; --------------- frame #3 ---------------
; Turning frame.
.obj3:
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID0, PF3_HEAVYHIT
		jp   .anim
; --------------- frame #5 ---------------
; Recovery on uppercut frame.
.chkEnd:
	mMvC_ValFrameEnd .anim
		call Play_Pl_EndMove
		jp   .ret
; --------------- common ---------------
.anim:
	call OBJLstS_DoAnimTiming_Loop_by_DE
.ret:
	ret
	
; =============== MoveC_Ralf_BackBreaker ===============
; Move code for Ralf's Super Argentine Back Breaker. (MOVE_RALF_BACK_BREAKER_L, MOVE_RALF_BACK_BREAKER_H)
MoveC_Ralf_BackBreaker:
	mMvC_ValLoaded .ret
	; Depending on the visible frame...
	mMvC_StartChkFrame
		mMvC_ChkFrame $00, .obj0
		mMvC_ChkFrame $01, .obj1
		mMvC_ChkFrame $02, .obj2
		mMvC_ChkFrame $03, .obj3
		mMvC_ChkFrame $04, .obj4
		mMvC_ChkFrame $05, .obj4
		mMvC_ChkFrame $06, .obj6
		mMvC_ChkFrame $07, .anim
		mMvC_ChkFrame $08, .chkEnd
; --------------- frame #0 ---------------
.obj0:
	; Throw the opponent up
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $05, HITTYPE_LAUNCH_SWOOPUP, $00
		jp   .anim
; --------------- frame #1 ---------------
.obj1:
	; Mid frame
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed $0F
		mMvC_SetDamageNext $01, HITTYPE_GRAB_UB_NOSYNC, $00
		jp   .anim
; --------------- frame #2 ---------------
.obj2:
	; When the frame ends... (which is quick enough)
	mMvC_ValFrameEnd .anim
		; ...wait indefinitely for the opponent to land back down.
		; Unlike most wait actions, this one has no timeout as it jumps to .ret.
		; Care must be taken to make sure the opponent isn't able to escape, otherwise
		; it will cause a softlock (which does happen on SFS).
		mMvC_ValHit .ret
		;--
		; Pointless
		ld   hl, iOBJInfo_FrameLeft
		add  hl, de
		ld   [hl], $00
		;--
		
		; Set next grab part
		mMvC_SetAnimSpeed $05
		mMvC_SetDamageNext $01, HITTYPE_GRAB_UB_NOSYNC, $00
		mMvC_PlaySound SCT_GROUNDHIT
		jp   .anim
; --------------- frame #3 ---------------
; Grab frame. Screen shake when the opponent lands on us.
.obj3:
	call Play_Pl_DoGroundScreenShake
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $01, HITTYPE_GRAB_UB_NOSYNC, $00
		jp   .anim
; --------------- frame #4-5 ---------------
; Grab frame.
.obj4:
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $01, HITTYPE_GRAB_UB_NOSYNC, $00
		jp   .anim
; --------------- frame #6 ---------------
; The actual throw dealing big damage.
.obj6:
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed $0A
		mMvC_SetDamageNext $14, HITTYPE_LAUNCH_FAST_DB, $00
		jp   .anim
; --------------- frame #8 ---------------
; Recovery.
.chkEnd:
	mMvC_ValFrameEnd .anim
		mMvC_EndThrow
		jr   .ret
; --------------- common ---------------
.anim:
	call OBJLstS_DoAnimTiming_Loop_by_DE
.ret:
	ret
	
; =============== MoveC_Ralf_BakudanPunch ===============
; Move code for Ralf's Kyuukouka Bakudan Punch. (MOVE_RALF_BAKUDAN_PUNCH_L, MOVE_RALF_BAKUDAN_PUNCH_H)
MoveC_Ralf_BakudanPunch:
	call Play_Pl_MoveByColiBoxOverlapX
	mMvC_ValLoaded .ret
	; Depending on the visible frame...
	mMvC_StartChkFrame
		mMvC_ChkFrame $00, .obj0
		mMvC_ChkFrame $01, .obj1
		mMvC_ChkFrame $02, .obj2
		mMvC_ChkFrame $03, .obj3
		mMvC_ChkFrame $04, .obj4
; --------------- frame #0 ---------------
; Startup
.obj0:
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed ANIMSPEED_NONE
		mMvC_PlaySound SFX_SPECIAL
		jp   .anim
; --------------- frame #1 ---------------
; Jump
.obj1:
	mMvC_ValFrameStartFast .obj1_cont
		;--
		ld   hl, iPlInfo_Flags0
		add  hl, bc
		set  PF0B_AIR, [hl]
		;--
		; Set jump speed
		mMvC_ChkMove MOVE_RALF_BAKUDAN_PUNCH_H, .obj1_setJumpH
	.obj1_setJumpL: ; Light
		mMvC_SetSpeedV -$0600
		jp   .obj1_cont
	.obj1_setJumpH: ; Heavy
		mMvC_ChkMaxPow .obj1_setJumpE
		mMvC_SetSpeedV -$0700
		jp   .obj1_cont
	.obj1_setJumpE: ; Max Power Heavy
		mMvC_SetSpeedV -$0800
.obj1_cont:
	; Wait for Y Speed > -$06 for next frame
	mMvC_NextFrameOnGtYSpeed -$06, ANIMSPEED_NONE
	jp   .doGravity
; --------------- frame #2 ---------------
; Pre-peak
.obj2:
	; Immediately go to the next frame (since we're already > -$06 here)
	mMvC_ValNextFrameOnGtYSpeed -$06, $03, .doGravity
		;--
		; No longer invulnerable shortly after the jump starts
		ld   hl, iPlInfo_Flags1
		add  hl, bc
		res  PF1B_INVULN, [hl]
		;--
		jp   .doGravity
; --------------- frame #3 ---------------
; Pre-peak
.obj3:
	; Prepare for gravity check
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed ANIMSPEED_NONE
		jp   .anim
; --------------- frame #4 ---------------
; Dive
.obj4:
	mMvC_ValFrameStartFast .doGravity
		; Set dive speed
		mMvC_ChkMove MOVE_RALF_BAKUDAN_PUNCH_H, .obj4_setDiveH
	.obj4_setDiveL: ; Light
		mMvC_SetSpeedH +$0200
		mMvC_SetSpeedV +$0200
		jp   .doGravity
	.obj4_setDiveH: ; Heavy
		mMvC_ChkMaxPow .obj4_setDiveE
		mMvC_SetSpeedH +$0400
		mMvC_SetSpeedV +$0300
		jp   .doGravity
	.obj4_setDiveE: ; Max Power Heavy
		mMvC_SetSpeedH +$0500
		mMvC_SetSpeedV +$0100
; --------------- common gravity check ---------------
.doGravity:
	; Backhop when landing
	mMvC_ChkGravityHV $0060, .anim
		;--
		; Allow special cancel
		ld   hl, iPlInfo_Flags0
		add  hl, bc
		res  PF0B_AIR, [hl]
		inc  hl
		res  PF1B_NOSPECSTART, [hl]
		;--
		mMvC_PlaySound SCT_GROUNDHIT
		
		; Transition to backjump
		ld   a, MOVE_SHARED_LAUNCH_UB_REC
		call Pl_SetMove_StopSpeed
		mMvC_SetSpeedH -$0300
		mMvC_SetSpeedV -$0500
		jp   .ret
; --------------- common ---------------
.anim:
	call OBJLstS_DoAnimTiming_Loop_by_DE
.ret:
	ret
	
; =============== MoveC_Ralf_BaribariVulcanPunch ===============
; Move code for Ralf's Baribari Vulcan Punch. (MOVE_RALF_BARIBARI_VULCAN_PUNCH_S)
MoveC_Ralf_BaribariVulcanPunch:
	call Play_Pl_MoveByColiBoxOverlapX
	mMvC_ValLoaded .ret
	; Depending on the visible frame...
	mMvC_StartChkFrame
		mMvC_ChkFrame $00, .obj0
		mMvC_ChkFrame $01, .obj1
		mMvC_ChkFrame $02, .obj2
		mMvC_ChkFrame $03, .obj3
		mMvC_ChkFrame $04, .obj4
		mMvC_ChkFrame $05, .obj5
		mMvC_ChkFrame $06, .obj6
		mMvC_ChkFrame $07, .obj5
		mMvC_ChkFrame $08, .obj6
		mMvC_ChkFrame $09, .obj9
		mMvC_ChkFrame $0A, .obj9
		mMvC_ChkFrame $0B, .objB
		mMvC_ChkFrame $0C, .chkEnd
		jp   .anim ; We never get here
; --------------- frame #0 ---------------
.obj0:
	mMvC_ValFrameEnd .anim
		mMvC_PlaySound SFX_HEAVY
		jp   .anim
; --------------- frame #1 ---------------
.obj1:
	mMvC_ValFrameStartFast .obj1_cont
		mMvC_SetSpeedH +$0500
.obj1_cont:
	mMvC_DoFrictionH +$0040
	mMvC_ValFrameEnd .anim
		mMvC_PlaySound SFX_HEAVY
		jp   .anim
; --------------- frame #3 ---------------
.obj3:
	mMvC_ValFrameStartFast .obj3_cont
		mMvC_SetSpeedH +$0500
.obj3_cont:
	mMvC_DoFrictionH +$0040
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID0, PF3_CONTHIT
		mMvC_PlaySound SFX_HEAVY
		jp   .anim
; --------------- frame #2 ---------------
.obj2:
	mMvC_ValFrameEnd .anim
	mMvC_SetDamageNext $08, HITTYPE_HIT_MID0, PF3_CONTHIT
		;--
		; Remove invuln
		ld   hl, iPlInfo_Flags1
		add  hl, bc
		res  PF1B_INVULN, [hl]
		;--
		mMvC_PlaySound SFX_HEAVY
		jp   .anim
; --------------- frame #4 ---------------
.obj4:
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed ANIMSPEED_INSTANT
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID1, PF3_FIRE|PF3_CONTHIT
		mMvC_PlaySound SFX_HEAVY
		jp   .anim
; --------------- frames #5,7 ---------------
.obj5:
	mMvC_ValFrameEnd .move
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID1, PF3_FIRE|PF3_CONTHIT
		mMvC_PlaySound SCT_GROUNDHIT
		jp   .move
; --------------- frames #6,8 ---------------
.obj6:
	mMvC_ValFrameEnd .move
		mMvC_SetDamageNext $08, HITTYPE_HIT_MID0, PF3_FIRE|PF3_CONTHIT
		mMvC_PlaySound SCT_GROUNDHIT
		jp   .move
; --------------- frames #9,A ---------------
.obj9:
	mMvC_ValFrameEnd .anim
		mMvC_SetDamageNext $08, HITTYPE_LAUNCH_HIGH_UB, PF3_HEAVYHIT|PF3_FIRE
		mMvC_PlaySound SCT_GROUNDHIT
; --------------- common forwards movement / frames #5-A ---------------
.move:
	mMvC_SetMoveH +$0200
	jp   .anim
; --------------- frame #B ---------------
.objB:
	mMvC_ValFrameEnd .anim
		mMvC_SetAnimSpeed $14
		mMvC_PlaySound SCT_GROUNDHIT
		jp   .anim
; --------------- frame #C ---------------
.chkEnd:
	mMvC_ValFrameEnd .anim
		call Play_Pl_EndMove
		jp   .ret
; --------------- common ---------------
.anim:
	call OBJLstS_DoAnimTiming_Loop_by_DE
.ret:
	ret
; =============== END OF BANK ===============
; Junk area below.
	mIncJunk "L197A25"